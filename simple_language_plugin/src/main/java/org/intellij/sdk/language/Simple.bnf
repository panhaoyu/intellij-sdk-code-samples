// Copyright 2000-2022 JetBrains s.r.o. and other contributors. Use of this source code is governed by the Apache 2.0 license that can be found in the LICENSE file.
{
  parserClass="org.intellij.sdk.language.parser.SimpleParser"
  parserUtilClass="org.intellij.sdk.language.parser.SimpleParserUtil"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Simple"
  psiImplClassSuffix="Impl"
  psiPackage="org.intellij.sdk.language.psi"
  psiImplPackage="org.intellij.sdk.language.psi.impl"

  elementTypeHolderClass="org.intellij.sdk.language.psi.SimpleTypes"
  elementTypeClass="org.intellij.sdk.language.psi.SimpleElementType"
  tokenTypeClass="org.intellij.sdk.language.psi.SimpleTokenType"

  psiImplUtilClass="org.intellij.sdk.language.psi.impl.SimplePsiImplUtil"
}

simpleFile ::= outerItem*

private outerItem ::= (outer_fish|function_define|comment_block|crlf|command_statement)

private outer_fish ::=  left_square_bracket single_statement right_square_bracket

function_define ::= function_define_header  (left_parenthesis function_parameter_list right_parenthesis)?  statement_block end
function_define_header ::= fish define function_name
private function_name ::= identifier
private function_parameter_list ::= (function_parameter (comma_operator function_parameter)* )?
private function_parameter ::= identifier

statement_block ::=  single_statement*

single_statement ::=  if_statement | loop_statement | case_statement  | assignment_statement | function_call_statement | comment_block | var_declare_statement | command_statement | identifier


private if_statement ::= if expression then statement_block else_if_statement* else_statement? endif
private else_if_statement ::= elseif expression then statement_block
private else_statement ::=  else statement_block

private loop_statement ::= loop statement_block endloop
private case_statement ::= case_of expression case_clause* end_case
private case_clause ::= case expression statement_block

private command_statement ::= command command_body* end_command
private any_in_command ::= (
    identifier | left_square_bracket | right_square_bracket |
    unary_operator | binary_operator| dot_operator|function_call_operator | literal | ellipsis | keywords_in_command_block
 )
private command_body ::= any_in_command+
private keywords_in_command_block ::= (fish | case_of | case | end_case | define | end | exit | global | if | then |
    elseif | else | endif | local | lock | loop | endloop | continue | return | section | end_section | struct  |)

private assignment_statement ::= identifier assignment_operator (expression | function_call_expression)

private local_var_declare_statement ::= local identifier (comma_operator identifier)* (assignment_operator expression)?
private global_var_declare_statement ::= global identifier (comma_operator identifier)* (assignment_operator expression)?
private var_declare_statement ::= local_var_declare_statement | global_var_declare_statement

private expression ::=  (left_parenthesis inner_expression right_parenthesis) | inner_expression
private inner_expression ::=  binary_expression | unary_expression | value | function_call_expression

private function_call_statement ::= function_call_expression
private function_call_expression ::= identifier argument_list?
private argument_list ::= left_parenthesis argument_expression (comma_operator argument_expression)* right_parenthesis
private argument_expression ::= function_call_expression | value

private binary_expression ::= value binary_operator value
private unary_expression ::= binary_operator expression


private comment_block ::= end_of_line_comment
value ::= identifier | literal

literal ::= STRING_LITERAL | NUMBER_LITERAL
private end_of_line_comment ::= COMMENT
private binary_operator ::= BINARY_OPERATOR
private unary_operator ::= UNARY_OPERATOR
private assignment_operator ::= ASSIGNMENT_OPERATOR
private crlf ::= CRLF
//noinspection BnfUnusedRule
private keyword ::= KEYWORD

private function_call_operator ::= FUNCTION_CALL_OPERATOR
private comma_operator ::= COMMA_OPERATOR
private dot_operator ::= DOT_OPERATOR

// Bracket Operators
private left_parenthesis ::= LEFT_PARENTHESIS
private right_parenthesis ::= RIGHT_PARENTHESIS
private left_square_bracket ::= LEFT_SQUARE_BRACKET
private right_square_bracket ::= RIGHT_SQUARE_BRACKET
private left_curly_bracket ::= LEFT_CURLY_BRACKET
private right_curly_bracket ::= RIGHT_CURLY_BRACKET
private  identifier ::= IDENTIFIER


property ::= (identifier) {
//  pin=3
  recoverWhile="recover_property"
  mixin="org.intellij.sdk.language.psi.impl.SimpleNamedElementImpl"
  implements="org.intellij.sdk.language.psi.SimpleNamedElement"
  methods=[getKey getValue getName setName getNameIdentifier getPresentation]
}
private recover_property ::= !(IDENTIFIER|COMMENT)


//keywords
private fish ::= FISH
private case_of ::= CASEOF
private case ::= CASE
private end_case ::= ENDCASE
private command ::= COMMAND
private end_command ::= ENDCOMMAND
private define ::= DEFINE
private end ::= END
private exit ::= EXIT
private global ::= GLOBAL
private if ::= IF
private then ::= THEN
private elseif ::= ELSEIF
private else ::= ELSE
private endif ::= ENDIF
private local ::= LOCAL
private lock ::= LOCK
private loop ::= LOOP
private endloop ::= ENDLOOP
private continue ::= CONTINUE
private return ::= RETURN
private section ::= SECTION
private end_section ::= ENDSECTION
private struct ::= STRUCT
private ellipsis ::=  ELLIPSIS
